---
subcategory: "Timestream for InfluxDB"
layout: "aws"
page_title: "AWS: aws_timestreaminfluxdb_db_cluster"
description: |-
  Terraform resource for managing an Amazon Timestream for InfluxDB read-replica cluster.
---

# Resource: aws_timestreaminfluxdb_db_cluster

Terraform resource for managing an Amazon Timestream for InfluxDB read-replica cluster.

~> **NOTE:** This resource requires a subscription to [Timestream for InfluxDB Read Replicas (Add-On) on the AWS Marketplace](https://aws.amazon.com/marketplace/pp/prodview-lftzfxtb5xlv4?applicationId=AWS-Marketplace-Console&ref_=beagle&sr=0-2).

## Example Usage

### Basic Usage

```terraform
resource "aws_timestreaminfluxdb_db_cluster" "example" {
  allocated_storage      = 20
  bucket                 = "example-bucket-name"
  db_instance_type       = "db.influx.medium"
  failover_mode          = "AUTOMATIC"
  username               = "admin"
  password               = "example-password"
  port                   = 8086
  organization           = "organization"
  vpc_subnet_ids         = [aws_subnet.example_1.id, aws_subnet.example_2.id]
  vpc_security_group_ids = [aws_security_group.example.id]
  name                   = "example-db-cluster"
}
```

### Usage with Prerequisite Resources

All Timestream for InfluxDB clusters require a VPC, at least two subnets, and a security group. The following example shows how these prerequisite resources can be created and used with `aws_timestreaminfluxdb_db_cluster`.

```terraform
resource "aws_vpc" "example" {
  cidr_block = "10.0.0.0/16"
}

resource "aws_subnet" "example_1" {
  vpc_id     = aws_vpc.example.id
  cidr_block = "10.0.1.0/24"
}

resource "aws_subnet" "example_2" {
  vpc_id     = aws_vpc.example.id
  cidr_block = "10.0.2.0/24"
}

resource "aws_security_group" "example" {
  name   = "example"
  vpc_id = aws_vpc.example.id
}

resource "aws_timestreaminfluxdb_db_cluster" "example" {
  allocated_storage      = 20
  bucket                 = "example-bucket-name"
  db_instance_type       = "db.influx.medium"
  username               = "admin"
  password               = "example-password"
  organization           = "organization"
  vpc_subnet_ids         = [aws_subnet.example_1.id, aws_subnet.example_2.id]
  vpc_security_group_ids = [aws_security_group.example.id]
  name                   = "example-db-cluster"
}
```

### Usage with Public Internet Access Enabled

The following configuration shows how to define the necessary resources and arguments to allow public internet access on your Timestream for InfluxDB read-replica cluster's primary endpoint (simply referred to as "endpoint") and read endpoint on port `8086`. After applying this configuration, the cluster's InfluxDB UI can be accessed by visiting your cluster's primary endpoint at port `8086`.

```terraform
resource "aws_vpc" "example" {
  cidr_block = "10.0.0.0/16"
}

resource "aws_subnet" "example_1" {
  vpc_id     = aws_vpc.example.id
  cidr_block = "10.0.1.0/24"
}

resource "aws_subnet" "example_2" {
  vpc_id     = aws_vpc.example.id
  cidr_block = "10.0.2.0/24"
}

resource "aws_security_group" "example" {
  name   = "example"
  vpc_id = aws_vpc.example.id
}

resource "aws_internet_gateway" "example" {
  vpc_id = aws_vpc.example.id

  tags = {
    Name = "example"
  }
}

resource "aws_route" "test_route" {
  route_table_id         = aws_vpc.example.main_route_table_id
  destination_cidr_block = "0.0.0.0/0"
  gateway_id             = aws_internet_gateway.example.id
}

resource "aws_route_table_association" "test_route_table_association" {
  subnet_id      = aws_subnet.test_subnet.id
  route_table_id = aws_vpc.example.main_route_table_id
}

resource "aws_vpc_security_group_ingress_rule" "example" {
  security_group_id            = aws_security_group.example.id
  referenced_security_group_id = aws_security_group.example.id
  ip_protocol                  = -1
}

resource "aws_vpc_security_group_ingress_rule" "example" {
  security_group_id = aws_security_group.example.id
  cidr_ipv4         = "0.0.0.0/0"
  ip_protocol       = "tcp"
  from_port         = 8086
  to_port           = 8086
}

resource "aws_timestreaminfluxdb_db_cluster" "example" {
  allocated_storage      = 20
  bucket                 = "example-bucket-name"
  db_instance_type       = "db.influx.medium"
  username               = "admin"
  password               = "example-password"
  organization           = "organization"
  vpc_subnet_ids         = [aws_subnet.example_1.id, aws_subnet.example_2.id]
  vpc_security_group_ids = [aws_security_group.example.id]
  name                   = "example-db-cluster"
  publicly_accessible    = true # False by default
}
```

### Usage with S3 Log Delivery Enabled

You can use an S3 bucket to store logs generated by your Timestream for InfluxDB cluster. The following example shows what resources and arguments are required to configure an S3 bucket for logging, including the IAM policy that needs to be set in order to allow Timestream for InfluxDB to place logs in your S3 bucket. The configuration of the required VPC, security group, and subnets have been left out of the example for brevity.

```terraform
resource "aws_s3_bucket" "example" {
  bucket        = "example-s3-bucket"
  force_destroy = true
}

data "aws_iam_policy_document" "example" {
  statement {
    actions = ["s3:PutObject"]
    principals {
      type        = "Service"
      identifiers = ["timestream-influxdb.amazonaws.com"]
    }
    resources = [
      "${aws_s3_bucket.example.arn}/*"
    ]
  }
}

resource "aws_s3_bucket_policy" "example" {
  bucket = aws_s3_bucket.example.id
  policy = data.aws_iam_policy_document.example.json
}

resource "aws_timestreaminfluxdb_db_cluster" "example" {
  allocated_storage      = 20
  bucket                 = "example-bucket-name"
  db_instance_type       = "db.influx.medium"
  username               = "admin"
  password               = "example-password"
  organization           = "organization"
  vpc_subnet_ids         = [aws_subnet.example_1.id, aws_subnet.example_2.id]
  vpc_security_group_ids = [aws_security_group.example.id]
  name                   = "example-db-cluster"

  log_delivery_configuration {
    s3_configuration {
      bucket_name = aws_s3_bucket.example.bucket
      enabled     = true
    }
  }
}
```

### Usage with InfluxDB V3

For InfluxDB V3 clusters, you can create a cluster without providing `allocated_storage`, `bucket`, `organization`, `username`, `password`, or `deployment_type` by specifying a `db_parameter_group_identifier` such as `"InfluxDBV3Core"`. The following example shows how to create an InfluxDB V3 cluster:

```terraform
resource "aws_timestreaminfluxdb_db_cluster" "example" {
  name                          = "example-v3-cluster"
  db_instance_type              = "db.influx.large"
  db_parameter_group_identifier = "InfluxDBV3Core"
  vpc_subnet_ids                = [aws_subnet.example_1.id, aws_subnet.example_2.id]
  vpc_security_group_ids        = [aws_security_group.example.id]
}
```

## Cluster Type Requirements

### InfluxDB V2 Clusters (default)

The following arguments are **required** for InfluxDB V2 clusters:

* `allocated_storage`
* `bucket`
* `deployment_type`
* `organization`
* `password`
* `username`

The `deployment_type` argument defaults to `"MULTI_NODE_READ_REPLICAS"` for InfluxDB V2 clusters when not specified.

### InfluxDB V3 Clusters (when using V3 parameter groups)

The following arguments are **forbidden** for InfluxDB V3 clusters (when using an InfluxDB V3 db parameter group):

* `allocated_storage`
* `bucket`
* `deployment_type`
* `organization`
* `password`
* `username`

## Argument Reference

The following arguments are required:

* `db_instance_type` - (Required) Timestream for InfluxDB DB instance type to run InfluxDB on. Valid options are: `"db.influx.medium"`, `"db.influx.large"`, `"db.influx.xlarge"`, `"db.influx.2xlarge"`, `"db.influx.4xlarge"`, `"db.influx.8xlarge"`, `"db.influx.12xlarge"`, and `"db.influx.16xlarge"`. This argument is updatable.
* `name` - (Required) Name that uniquely identifies the DB cluster when interacting with the Amazon Timestream for InfluxDB API and CLI commands. This name will also be a prefix included in the endpoint. Cluster names must be unique per customer and per region. The argument must start with a letter, cannot contain consecutive hyphens (`-`) and cannot end with a hyphen.
* `vpc_security_group_ids` - (Required) List of VPC security group IDs to associate with the cluster.
* `vpc_subnet_ids` - (Required) List of VPC subnet IDs to associate with the cluster. Provide at least two VPC subnet IDs in different availability zones when deploying with a Multi-AZ standby.

The following arguments are optional:

* `allocated_storage` - (Optional) Amount of storage in GiB (gibibytes). The minimum value is `20`, the maximum value is `16384`. The argument `db_storage_type` places restrictions on this argument's minimum value. The following is a list of `db_storage_type` values and the corresponding minimum value for `allocated_storage`: `"InfluxIOIncludedT1": `20`, `"InfluxIOIncludedT2" and `"InfluxIOIncludedT3": `400`. This field is forbidden for InfluxDB V3 clusters (when using an InfluxDB V3 db parameter group).
* `bucket` - (Optional) Name of the initial InfluxDB bucket. All InfluxDB data is stored in a bucket. A bucket combines the concept of a database and a retention period (the duration of time that each data point persists). A bucket belongs to an organization. Along with `organization`, `username`, and `password`, this argument will be stored in the secret referred to by the `influx_auth_parameters_secret_arn` attribute. This field is forbidden for InfluxDB V3 clusters (when using an InfluxDB V3 db parameter group).
* `db_parameter_group_identifier` - (Optional) ID of the DB parameter group assigned to your cluster. This argument is updatable. If added to an existing Timestream for InfluxDB cluster or given a new value, will cause an in-place update to the cluster. However, if a cluster already has a value for `db_parameter_group_identifier`, removing `db_parameter_group_identifier` will cause the cluster to be destroyed and recreated.
* `db_storage_type` - (Default `"InfluxIOIncludedT1"`) Timestream for InfluxDB DB storage type to read and write InfluxDB data. You can choose between 3 different types of provisioned Influx IOPS included storage according to your workloads requirements: Influx IO Included 3000 IOPS, Influx IO Included 12000 IOPS, Influx IO Included 16000 IOPS. Valid options are: `"InfluxIOIncludedT1"`, `"InfluxIOIncludedT2"`, and `"InfluxIOIncludedT3"`. If you use `"InfluxIOIncludedT2" or "InfluxIOIncludedT3", the minimum value for `allocated_storage` is 400.
* `deployment_type` - (Default `"MULTI_NODE_READ_REPLICAS"` for InfluxDB V2 clusters) Specifies the type of cluster to create. Valid options are: `"MULTI_NODE_READ_REPLICAS"`. This field is forbidden for InfluxDB V3 clusters (when using an InfluxDB V3 db parameter group).
* `failover_mode` - (Default `"AUTOMATIC"`) Specifies the behavior of failure recovery when the primary node of the cluster fails. Valid options are: `"AUTOMATIC"` and `"NO_FAILOVER"`.
* `log_delivery_configuration` - (Optional) Configuration for sending InfluxDB engine logs to a specified S3 bucket. This argument is updatable.
* `network_type` - (Optional) Specifies whether the network type of the Timestream for InfluxDB cluster is IPV4, which can communicate over IPv4 protocol only, or DUAL, which can communicate over both IPv4 and IPv6 protocols.
* `organization` - (Optional) Name of the initial organization for the initial admin user in InfluxDB. An InfluxDB organization is a workspace for a group of users. Along with `bucket`, `username`, and `password`, this argument will be stored in the secret referred to by the `influx_auth_parameters_secret_arn` attribute. This field is forbidden for InfluxDB V3 clusters (when using an InfluxDB V3 db parameter group).
* `password` - (Optional) Password of the initial admin user created in InfluxDB. This password will allow you to access the InfluxDB UI to perform various administrative tasks and also use the InfluxDB CLI to create an operator token. Along with `bucket`, `username`, and `organization`, this argument will be stored in the secret referred to by the `influx_auth_parameters_secret_arn` attribute. This field is forbidden for InfluxDB V3 clusters (when using an InfluxDB V3 db parameter group) as the AWS API rejects it.
* `port` - (Default `8086`) The port on which the cluster accepts connections. Valid values: `1024`-`65535`. Cannot be `2375`-`2376`, `7788`-`7799`, `8090`, or `51678`-`51680`. This argument is updatable.
* `publicly_accessible` - (Default `false`) Configures the DB cluster with a public IP to facilitate access. Other resources, such as a VPC, a subnet, an internet gateway, and a route table with routes, are also required to enabled public access, in addition to this argument. See "[Usage with Public Internet Access Enabled](#usage-with-public-internet-access-enabled)" for an example configuration with all required resources for public internet access.
* `region` - (Optional) Region where this resource will be [managed](https://docs.aws.amazon.com/general/latest/gr/rande.html#regional-endpoints). Defaults to the Region set in the [provider configuration](https://registry.terraform.io/providers/hashicorp/aws/latest/docs#aws-configuration-reference).
* `tags` - (Optional) Map of tags assigned to the resource. If configured with a provider [`default_tags` configuration block](/docs/providers/aws/index.html#default_tags-configuration-block) present, tags with matching keys will overwrite those defined at the provider-level.
* `username` - (Optional) Username of the initial admin user created in InfluxDB. Must start with a letter and can't end with a hyphen or contain two consecutive hyphens. This username will allow you to access the InfluxDB UI to perform various administrative tasks and also use the InfluxDB CLI to create an operator token. Along with `bucket`, `organization`, and `password`, this argument will be stored in the secret referred to by the `influx_auth_parameters_secret_arn` attribute. This field is forbidden for InfluxDB V3 clusters (when using an InfluxDB V3 db parameter group).

### Nested Fields

#### `log_delivery_configuration`

* `s3_configuration` - (Required) Configuration for S3 bucket log delivery.

#### `s3_configuration`

* `bucket_name` - (Required) Name of the S3 bucket to deliver logs to.
* `enabled` - (Required) Indicates whether log delivery to the S3 bucket is enabled.

**Note**: The following arguments do updates in-place: `db_parameter_group_identifier`, `log_delivery_configuration`, `port`, `db_instance_type`, `failover_mode`, and `tags`. Changes to any other argument after a cluster has been deployed will cause destruction and re-creation of the cluster. Additionally, when `db_parameter_group_identifier` is added to a cluster or modified, the cluster will be updated in-place but if `db_parameter_group_identifier` is removed from a cluster, the cluster will be destroyed and re-created.

## Attribute Reference

This resource exports the following attributes in addition to the arguments above:

* `arn` - ARN of the Timestream for InfluxDB cluster.
* `engine_type` - Database engine type of the DB cluster.
* `endpoint` - Endpoint used to connect to InfluxDB. The default InfluxDB port is 8086.
* `id` - ID of the Timestream for InfluxDB cluster.
* `influx_auth_parameters_secret_arn` - ARN of the AWS Secrets Manager secret containing the initial InfluxDB authorization parameters. For InfluxDB V2 clusters, the secret value is a JSON formatted key-value pair holding InfluxDB authorization values: organization, bucket, username, and password. For InfluxDB V3 clusters, the secret contains the InfluxDB admin token.
* `reader_endpoint` - The endpoint used to connect to the Timestream for InfluxDB cluster for read-only operations.
* `tags_all` - Map of tags assigned to the resource, including those inherited from the provider [`default_tags` configuration block](https://registry.terraform.io/providers/hashicorp/aws/latest/docs#default_tags-configuration-block).

## Timeouts

[Configuration options](https://developer.hashicorp.com/terraform/language/resources/syntax#operation-timeouts):

* `create` - (Default `30m`)
* `update` - (Default `30m`)
* `delete` - (Default `30m`)

## Import

In Terraform v1.5.0 and later, use an [`import` block](https://developer.hashicorp.com/terraform/language/import) to import Timestream for InfluxDB cluster using its identifier. For example:

```terraform
import {
  to = aws_timestreaminfluxdb_db_cluster.example
  id = "12345abcde"
}
```

Using `terraform import`, import Timestream for InfluxDB cluster using its identifier. For example:

```console
% terraform import aws_timestreaminfluxdb_db_cluster.example 12345abcde
```
